plugins {
  id 'com.android.application'
  id 'org.jetbrains.kotlin.android'
  id 'kotlin-kapt'
}

android {
  namespace 'com.kodeco.android.dogbreedsapp'
  compileSdk 34

  defaultConfig {
    applicationId "com.kodeco.android.dogbreedsapp"
    minSdk 24
    targetSdk 34
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    vectorDrawables {
      useSupportLibrary true
    }
    testOptions {
      unitTests {
        includeAndroidResources = true
      }
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_19
    targetCompatibility JavaVersion.VERSION_19
  }
  kotlinOptions {
    jvmTarget = '19'
  }
  buildFeatures {
    compose true
  }
  composeOptions {
    kotlinCompilerExtensionVersion '1.5.1'
  }
  packagingOptions {
    resources {
      excludes += '/META-INF/{AL2.0,LGPL2.1}'
    }
  }
}

dependencies {
  constraints {
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
      because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
    }
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
      because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
    }
  }

  // Compose
  def composeBom = platform('androidx.compose:compose-bom:2022.12.00')
  implementation composeBom
  androidTestImplementation composeBom
  implementation 'androidx.compose.material3:material3'
  implementation "androidx.compose.ui:ui"
  implementation 'androidx.activity:activity-compose:1.7.2'
  implementation "androidx.compose.ui:ui-tooling-preview"
  debugImplementation "androidx.compose.ui:ui-tooling"

  // SplashScreen
  implementation 'androidx.core:core-splashscreen:1.0.1'

  implementation 'androidx.core:core-ktx:1.10.1'

  // Retrofit
  implementation 'com.squareup.retrofit2:retrofit:2.9.0'

  // Moshi
  implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
  implementation("com.squareup.moshi:moshi-kotlin:1.14.0")

  // Koin
  implementation "io.insert-koin:koin-androidx-compose:3.4.2"

  // logcat
  implementation 'com.squareup.logcat:logcat:0.1'

  // Coroutines
  def coroutineVersion = "1.6.4"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"

  // Coil
  implementation("io.coil-kt:coil-compose:2.2.2")

  // Lifecycle runtime
  def lifecycle_version = "2.6.1"
  implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_version"

  // Okhttp3
  implementation("com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.10")

  // Room
  def room_version = "2.5.2"
  implementation "androidx.room:room-runtime:$room_version"
  implementation "androidx.room:room-ktx:$room_version"
  kapt "androidx.room:room-compiler:$room_version"


  // Testing
  testImplementation "junit:junit:4.13.2"
  androidTestImplementation "junit:junit:4.13.2"
  testImplementation "com.google.truth:truth:1.1.3"
  androidTestImplementation "com.google.truth:truth:1.1.3"
  testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
  androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.5.0'
  debugImplementation 'androidx.compose.ui:ui-test-manifest:1.5.0'

  // TUTORIAL DEPENDENCIES
  def nav_version = "2.7.0"
  implementation "androidx.navigation:navigation-compose:$nav_version"
  androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"

}